! ------------------------------------------------------------------------------------------------------
! Setup order in which aggregation is performed over mesh decomposition
! ------------------------------------------------------------------------------------------------------
SUBROUTINE SCARC_SETUP_AGGREGATION_ORDER
USE SCARC_POINTERS, ONLY: SUB, S
INTEGER, ALLOCATABLE, DIMENSION(:) :: INT_BUFFER
INTEGER, ALLOCATABLE, DIMENSION(:) :: COUNTS_NBR   
INTEGER, ALLOCATABLE, DIMENSION(:) :: DISPLS_NBR    
LOGICAL, ALLOCATABLE, DIMENSION(:) :: NOT_AGGREGATED
INTEGER :: N, NM, NOM, INBR, MAX_NBR, IP, ICYCLE

SUB => SUBDIVISION

CALL SCARC_ALLOCATE_INT1 (SUB%N_NEIGHBORS, 1, NMESHES, NSCARC_INIT_ZERO, 'SUB%N_NEIGHBORS')
DO NM = LOWER_MESH_INDEX, UPPER_MESH_INDEX
   SUB%N_NEIGHBORS(NM) = SCARC(NM)%N_NEIGHBORS
ENDDO

IF (N_MPI_PROCESSES > 1) &
   CALL MPI_ALLGATHERV(MPI_IN_PLACE,1,MPI_INTEGER,SUB%N_NEIGHBORS,COUNTS,DISPLS, MPI_INTEGER,MPI_COMM_WORLD,IERROR)

SUB%N_NEIGHBORS_TOTAL = SUM(SUB%N_NEIGHBORS)

#ifdef WITH_SCARC_DEBUG
WRITE(MSG%LU_DEBUG,*) 'SUB%N_NEIGHBORS:'
WRITE(MSG%LU_DEBUG,*) SUB%N_NEIGHBORS
WRITE(MSG%LU_DEBUG,*) SUB%N_NEIGHBORS_TOTAL
#endif

CALL SCARC_ALLOCATE_INT1 (COUNTS_NBR, 0, N_MPI_PROCESSES-1, NSCARC_INIT_ZERO, 'COUNTS_NBR')
CALL SCARC_ALLOCATE_INT1 (DISPLS_NBR, 0, N_MPI_PROCESSES-1, NSCARC_INIT_ZERO, 'DISPLS_NBR')

DO N = 0, N_MPI_PROCESSES - 1
   DO NM = 1, NMESHES
      IF (PROCESS(NM) == N) COUNTS_NBR(N) = COUNTS_NBR(N) + SUB%N_NEIGHBORS(NM)
   ENDDO
ENDDO

DO N = 1, N_MPI_PROCESSES -1
   DISPLS_NBR(N) = COUNTS_NBR(N-1) + DISPLS_NBR(N-1)
ENDDO

#ifdef WITH_SCARC_DEBUG
WRITE(MSG%LU_DEBUG,*) 'COUNTS_NBR:'
WRITE(MSG%LU_DEBUG,*) COUNTS_NBR
WRITE(MSG%LU_DEBUG,*) 'DISPLS_NBR:'
WRITE(MSG%LU_DEBUG,*) DISPLS_NBR
#endif
  
CALL SCARC_ALLOCATE_INT1 (INT_BUFFER, 1, SUB%N_NEIGHBORS_TOTAL, NSCARC_INIT_ZERO, 'INT_BUFFER')

IP = 1
DO NM = LOWER_MESH_INDEX, UPPER_MESH_INDEX
   DO INBR = 1, SCARC(NM)%N_NEIGHBORS
      INT_BUFFER(DISPLS_NBR(NM-1) + IP) = SCARC(NM)%NEIGHBORS(INBR)
      IP = IP + 1
   ENDDO
ENDDO

#ifdef WITH_SCARC_DEBUG
WRITE(MSG%LU_DEBUG,*) 'INT_BUFFER_SEND on mesh ', MYID+1
WRITE(MSG%LU_DEBUG,*) INT_BUFFER
#endif


IF (N_MPI_PROCESSES > 1) &
   CALL MPI_ALLGATHERV(MPI_IN_PLACE,1,MPI_INTEGER,INT_BUFFER,COUNTS_NBR,DISPLS_NBR, MPI_INTEGER,MPI_COMM_WORLD,IERROR)


MAX_NBR = MAXVAL(SUB%N_NEIGHBORS)
#ifdef WITH_SCARC_DEBUG
WRITE(MSG%LU_DEBUG,*) 'MAX_NBR ', MAX_NBR
WRITE(MSG%LU_DEBUG,*) 'INT_BUFFER_RECV on mesh NM, MAX_NBR ', MYID+1
WRITE(MSG%LU_DEBUG,*) INT_BUFFER
#endif

CALL SCARC_ALLOCATE_INT2 (SUB%NEIGHBORS, 1, MAX_NBR, 1, NMESHES,  NSCARC_INIT_ZERO, 'SUB%NEIGHBORS')
DO NM = 1, NMESHES
   DO INBR = 1, SUB%N_NEIGHBORS(NM)
      SUB%NEIGHBORS(INBR, NM) = INT_BUFFER(DISPLS_NBR(NM-1) + INBR)
   ENDDO
#ifdef WITH_SCARC_DEBUG
WRITE(MSG%LU_DEBUG,*) 'NM: SUB%NEIGHBORS:', SUB%NEIGHBORS(1:SUB%N_NEIGHBORS(NM),NM)
#endif
ENDDO

DEALLOCATE(INT_BUFFER)
DEALLOCATE(COUNTS_NBR)
DEALLOCATE(DISPLS_NBR)


CALL SCARC_ALLOCATE_INT2(SUB%ORDER, 1, NMESHES, 1, NMESHES, NSCARC_INIT_NONE, 'SUB%ORDER')
CALL SCARC_ALLOCATE_LOG1(NOT_AGGREGATED, 1, NMESHES, NSCARC_INIT_TRUE, 'SUB%IS_FINISHED')

ICYCLE = 1
DO WHILE (ANY(NOT_AGGREGATED)) 
#ifdef WITH_SCARC_DEBUG
WRITE(MSG%LU_DEBUG,*) '----------- ICYCLE, NOT_AGGREGATED:', ICYCLE, NOT_AGGREGATED
#endif
   SUB%ORDER(1:NMESHES, ICYCLE) = NSCARC_ORDER_UNDEFINED
   DO NM = 1, NMESHES
      S => SCARC(NM)
      IF (NOT_AGGREGATED(NM) .AND. SUB%ORDER(NM, ICYCLE) /= NSCARC_ORDER_LOCKED) THEN
#ifdef WITH_SCARC_DEBUG
WRITE(MSG%LU_DEBUG,*) '          SUB%ORDER(',NM,', ICYCLE)=', SUB%ORDER(NM, ICYCLE)
#endif
         SUB%ORDER(NM, ICYCLE) = NSCARC_ORDER_AGGREGATING
         DO INBR = 1, S%N_NEIGHBORS
            NOM = S%NEIGHBORS(INBR)
            SUB%ORDER(NOM, ICYCLE) = NSCARC_ORDER_LOCKED
#ifdef WITH_SCARC_DEBUG
WRITE(MSG%LU_DEBUG,*) '          SUB%ORDER(',NOM,', ICYCLE)=', SUB%ORDER(NOM, ICYCLE)
#endif
         ENDDO
         NOT_AGGREGATED(NM) = .FALSE.
      ENDIF
   ENDDO

   ICYCLE = ICYCLE + 1
ENDDO

SUB%N_CYCLES = ICYCLE - 1
#ifdef WITH_SCARC_DEBUG
WRITE(MSG%LU_DEBUG,*) 'SUB%N_CYCLES :', SUB%N_CYCLES
DO ICYCLE = 1, SUB%N_CYCLES
   WRITE(MSG%LU_DEBUG,*) SUB%ORDER(1:NMESHES, ICYCLE)
ENDDO
#endif

DEALLOCATE(NOT_AGGREGATED)

END SUBROUTINE SCARC_SETUP_AGGREGATION_ORDER

